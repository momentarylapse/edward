const string KABA_LINK = "/usr/lib/libpsmoveapi.so
	_psmove_init:psmove_init
	_psmove_count_connected:psmove_count_connected
	_psmove_reinit:psmove_reinit
	_psmove_connect_by_id:psmove_connect_by_id
	_psmove_connect:psmove_connect
	_psmove_connection_type:psmove_connection_type
	_psmove_disconnect:psmove_disconnect
	_psmove_pair:psmove_pair
	_psmove_get_serial:psmove_get_serial
	_psmove_set_leds:psmove_set_leds
	_psmove_update_leds:psmove_update_leds
	_psmove_set_rumble:psmove_set_rumble
	_psmove_poll:psmove_poll
	_psmove_get_buttons:psmove_get_buttons
	_psmove_get_battery:psmove_get_battery
	_psmove_get_trigger:psmove_get_trigger
	_psmove_enable_orientation:psmove_enable_orientation
	_psmove_has_orientation:psmove_has_orientation
	_psmove_reset_orientation:psmove_reset_orientation
	_psmove_get_accelerometer_frame:psmove_get_accelerometer_frame
	_psmove_get_gyroscope_frame:psmove_get_gyroscope_frame
/usr/lib/libpsmoveapi_tracker.so
	_psmove_tracker_new:psmove_tracker_new
	_psmove_tracker_free:psmove_tracker_free
	_psmove_tracker_set_mirror:psmove_tracker_set_mirror
	_psmove_tracker_set_exposure:psmove_tracker_set_exposure
	_psmove_tracker_enable:psmove_tracker_enable
	_psmove_tracker_update_image:psmove_tracker_update_image
	_psmove_tracker_update:psmove_tracker_update
	_psmove_tracker_update_image:psmove_tracker_update_image
	_psmove_fusion_new:psmove_fusion_new
	_psmove_fusion_free:psmove_fusion_free
	_psmove_fusion_get_position:psmove_fusion_get_position
	_psmove_fusion_get_modelview_matrix:psmove_fusion_get_modelview_matrix"


class PSMoveTracker
class PSMoveFusion
class PSMove

extern int _psmove_count_connected()
extern int _psmove_init(int version)
extern void _psmove_reinit()

extern PSMove* _psmove_connect()
extern PSMove* _psmove_connect_by_id(int i)
extern int _psmove_connection_type(PSMove* p)
extern void _psmove_disconnect(PSMove* move)
extern char[0]* _psmove_get_serial(PSMove* p)
extern int _psmove_pair(PSMove* move)
extern void _psmove_set_leds(PSMove* p, char r, char g, char b)
extern int _psmove_update_leds(PSMove* p)
extern void _psmove_set_rumble(PSMove* p, char rumble)
extern void _psmove_enable_orientation(PSMove* p, int m)
extern int _psmove_has_orientation(PSMove* p)
extern void _psmove_reset_orientation(PSMove* p)
extern int _psmove_poll(PSMove* p)
extern int _psmove_get_buttons(PSMove* p)
extern int _psmove_get_battery(PSMove* p)
extern int _psmove_get_trigger(PSMove* p)
extern void _psmove_get_accelerometer_frame(PSMove *move, int frame, float *ax, float *ay, float *az)
extern void _psmove_get_gyroscope_frame(PSMove *move, int frame, float *gx, float *gy, float *gz)
# frame 1 = Frame_SecondHalf

extern PSMoveTracker* _psmove_tracker_new()
extern void _psmove_tracker_free(PSMoveTracker* tracker)
extern void _psmove_tracker_set_mirror(PSMoveTracker* tracker, int m)
extern void _psmove_tracker_set_exposure(PSMoveTracker* tracker, int m)
extern int _psmove_tracker_enable(PSMoveTracker* tracker, PSMove* p)
extern int _psmove_tracker_update(PSMoveTracker* tracker, PSMove* move)
extern void _psmove_tracker_update_image(PSMoveTracker* tracker)

extern PSMoveFusion* _psmove_fusion_new(PSMoveTracker* tracker, float a, float b)
extern void _psmove_fusion_free(PSMoveFusion* fusion)
extern void _psmove_fusion_get_position(PSMoveFusion *fusion, PSMove *psmove, float *x, float *y, float *z)
extern matrix* _psmove_fusion_get_modelview_matrix(PSMoveFusion* fusion, PSMove* move)


enum
	Btn_TRIANGLE = 16 # Green triangle
	Btn_CIRCLE = 32 # Red circle
	Btn_CROSS = 64 # Blue cross
	Btn_SQUARE = 128 # Pink square
	Btn_SELECT = 256 # Select button, left side
	Btn_START = 2048 # Start button, right side

	Btn_PS = 65536 # PS button, front center
	Btn_MOVE = 524288 # Move button, big front button
	Btn_T = 1048576 # Trigger, on the back

class PsMoveController
	PSMove* psmove
	vector pos, dpos, pos_last
	vector acc, rot, mag
	color col
	quaternion ang, dang
	float trigger, rumble
	bool[8] button
	PsMoveManager* manager
	void __init__(PSMove *m, PsMoveManager man)
		manager = &man
		psmove = m
		_psmove_enable_orientation(m, 1)
		if _psmove_has_orientation(m) == 0
			print(" ERROR: no orientation")
		while _psmove_tracker_enable(manager.tracker, m) != 2 # Tracker_CALIBRATED
			pass
		col = Black
		rumble = 0
		trigger = 0
		ang = quaternion.ID
		dang = quaternion.ID

	void update()
		if !psmove
			return

		while _psmove_poll(psmove) != 0
			pass

		_psmove_fusion_get_position(manager.fusion, psmove, &pos.x, &pos.y, &pos.z)
		pos.y = - pos.y

		dpos = pos - pos_last
		pos_last = pos

		matrix *mat = _psmove_fusion_get_modelview_matrix(manager.fusion, psmove)
		matrix s = matrix.scale(1,  1,  -1)
		quaternion q = quaternion(s * *mat * s)
		dang = ang * q
		ang = q.bar()
		#ang = quaternion::rotation_m( *mat)

		_psmove_set_rumble(psmove, int(rumble*255))
		trigger = float(_psmove_get_trigger(psmove)) / 255.0

		_psmove_get_accelerometer_frame(psmove, 1, &acc.x, &acc.y, &acc.z)
		acc.y = - acc.y
		_psmove_get_gyroscope_frame(psmove, 1, &rot.x, &rot.y, &rot.z)
		rot.y = - rot.y

		int _buttons = _psmove_get_buttons(psmove)
		button[0] = ((_buttons & Btn_TRIANGLE) != 0)
		button[1] = ((_buttons & Btn_CIRCLE) != 0)
		button[2] = ((_buttons & Btn_CROSS) != 0)
		button[3] = ((_buttons & Btn_SQUARE) != 0)
		button[4] = ((_buttons & Btn_SELECT) != 0)
		button[5] = ((_buttons & Btn_START) != 0)
		button[6] = ((_buttons & Btn_PS) != 0)
		button[7] = ((_buttons & Btn_MOVE) != 0)

	void reset_orientation()
		_psmove_reset_orientation(psmove)
		ang = quaternion.ID

class PsMoveManager
	PSMoveTracker* tracker
	PSMoveFusion* fusion
	PsMoveController*[] psmoves
	void __init__()
		tracker = nil
		fusion = nil
		int vv = 262152 #4<<16 + 0<<8 + 8
		if _psmove_init(vv) != 1
			print("VERSION MISMATCH!")
	void __delete()
		psmoves.clear()
		if fusion
			_psmove_fusion_free(fusion)
		if tracker
			_psmove_tracker_free(tracker)
	void connect()
		int n = _psmove_count_connected()
		print("{{n}} found")
		tracker = _psmove_tracker_new()
		fusion = _psmove_fusion_new(tracker, 1., 1000.)
		_psmove_tracker_set_mirror(tracker, 1)
		_psmove_tracker_set_exposure(tracker, 0)
		for i in 0:n
			print("- controller {{i}} -")
			#let p = _psmove_connect()
			let p = _psmove_connect_by_id(i)
			psmoves.add(new PsMoveController(p, self))

	void update()
		if !tracker
			return
		for m in psmoves
			m.update()

		_psmove_tracker_update_image(tracker)
		_psmove_tracker_update(tracker, nil)


void main()
	PsMoveManager m
	m.connect()
	
	while true
		m.update()
