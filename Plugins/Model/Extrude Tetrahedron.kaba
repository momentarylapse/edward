use "../api.kaba"



void main()
	data_model.BeginActionGroup("ExtrudeTetrahedron")
	
	int[] surf, poly
	int nv = len(data_model.vertex)
	int[][] vv
	
	for s,is in data_model.surface
		for p,ip in s.polygon
			if p.is_selected and len(p.side) == 3
				surf.add(is)
				poly.add(ip)
				
				let v = [for side in p.side side.vertex]
				vv.add(v)
				let q = [for vi in v data_model.vertex[vi].pos]
				float R = ((q[1]-q[0]).length() + (q[2]-q[1]).length() + (q[0]-q[2]).length()) / 3
				float h = sqrt(2.0/3) * R
	
				data_model.AddVertex((q[0]+q[1]+q[2])/3 + p.temp_normal * h, 0, 1)
	
	data_model.ClearSelection()
	for i in 0:len(surf)
		data_model.surface[surf[i]].polygon[poly[i]].is_selected = true
	data_model.DeleteSelection(false)
	
	for v,i in vv
		data_model.AddTriangle(v[0], v[1], nv+i, 0)
		data_model.AddTriangle(v[1], v[2], nv+i, 0)
		data_model.AddTriangle(v[2], v[0], nv+i, 0)
	
	data_model.EndActionGroup()

	